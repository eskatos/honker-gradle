/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven'
    id 'eclipse'
    id 'idea'
    id "org.nosphere.apache.rat" version "0.3.0"
    id "org.nosphere.honker" version "0.3.0"
}

repositories {
    jcenter()
}

dependencies {
    compile 'commons-lang:commons-lang:2.6'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'com.google.guava:guava:18.0'
    testCompile 'com.netflix.nebula:nebula-test:7.0.0', {
        exclude module: 'groovy-all'
    }
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

sourceSets {
    main {
        output.dir( honkerGenDependencies.outputDir, builtBy: honkerGenDependencies )
        output.dir( honkerGenLicense.outputDir, builtBy: honkerGenLicense )
        output.dir( honkerGenNotice.outputDir, builtBy: honkerGenNotice )
    }
}
honkerGenNotice { footer = 'This product includes software developed at\nThe Apache Software Foundation (http://www.apache.org/).\n' }
honker.license = 'Apache 2'
check.dependsOn honkerCheck

rat {
    excludes += [
        'README.md',
        '.gradletasknamecache', 'gradle/wrapper/**', 'gradlew*', 'build/**', // Gradle
        '.nb-gradle/**', '*.iml', '*.ipr', '*.iws', // IDEs
        '**/*.template' // Templates
    ]
}
check.dependsOn rat

apply plugin: "com.gradle.plugin-publish"
pluginBundle {
    website = 'https://github.com/eskatos/honker-gradle'
    vcsUrl = 'https://github.com/eskatos/honker-gradle'
    description = 'The Honker Gradle Plugin help assemble legal bits'
    tags = ['licensing']

    plugins {
        honkerPlugin {
            id = 'org.nosphere.honker'
            displayName = 'Honker Gradle Plugin'
            tags = ['NOTICE','LICENSE','DEPENDENCIES']
        }
    }
}
